node {
    version = versions.node
    download = true
}

task upgradePackageDependencies(type: NodeTask) {
    script = project.file('node_modules/npm-check-updates/bin/npm-check-updates')
    args = ['-u', '--packageFile', file('package.json')]
}

task manifest(dependsOn: npm_run_build) << {
    def scmBranch = 'unknown'
    def scmRevision = 'unknown'

    try {
        scmBranch = 'git branch --contains'.execute().text.trim() - '* '
        scmRevision = 'git rev-parse HEAD'.execute().text.trim()
    } catch (Exception ignored) {
    }

    def json = new groovy.json.JsonBuilder()
    json (
        [
            'Implementation-Title': applicationName,
            'Implementation-Version': project.version,
            'SCM-Branch': scmBranch,
            'SCM-Revision': scmRevision,
            'Build-Timestamp': new java.text.SimpleDateFormat('yyyy/MM/dd HH:mm:ss.SSS').format(new Date()),
            'Jenkins-Build-Number': System.getenv()['BUILD_NUMBER'] ?: ''
        ]
    )

    file('dist/manifest.json').text = json.toString()
}

task cacheKey(dependsOn: npm_run_build) << {
    def scmRevision = 'unknown'

    try {
        scmRevision = 'git rev-parse HEAD'.execute().text.trim()
    } catch (Exception e) {
    }

    def json = new groovy.json.JsonBuilder()
    json (
        [
            'cacheKey': scmRevision
        ]
    )

    file('dist/cacheKey.json').text = json.toString()
}

task cleanDist() << {
    project.delete file('./dist/').listFiles()
}

tasks['npm_run_build'].dependsOn('cleanDist')

['classes' : ['npm_run_build', 'manifest', 'cacheKey'], 'clean' : 'npm_cache_clean'].each {
    if (!tasks.findByName(it.key)) {
        task(it.key) {

        }
    }

    tasks[it.key].dependsOn(it.value)
}

tasks['refreshResource'].dependsOn('npmInstall')
